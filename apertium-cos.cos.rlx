# Firstly, we need to define what tags should be considered sentence delimiters. For this example, only full stop is set as delimiter.
DELIMITERS = "<.>" ;

SUBREADINGS = LTR ; 

LIST Det = det ;
LIST Pr = pr ; 

LIST Mod = vbmod ;
LIST Inf = inf ;

LIST Pos = pos ;

LIST Enc = enc ; 
LIST Pro = pro ;

LIST Sg = sg ;

LIST Fin = pri prs pii pis fti ifi ; 

LIST Unk = "<\\*.*>"r ;

LIST L' = "<l'>"i ;
LIST A = "<a>"i ; 
LIST DI = "<di>"i ; 
LIST SI = "<si>"i ; 
LIST UN = "<un>"i ;
LIST DÌ = "dì" ; 
LIST SÌ = "sì" ; 
LIST ÙN = "ùn" ; 
LIST Di = "di" ; 
LIST Vene = "vene" ; 

LIST IN = "in" ; 

LIST Lingue = "francesu" "francese" "danese" "inglese" "inglesu" "talianu" ; 

LIST Inf/à = ("<.*[Àà]>"r inf) ;
LIST Form/a = ("<.*[Aa]>"r inf) ;

SECTION

SELECT Pr IF (0 A) (1 A) ;

REMOVE DÌ IF (0 DI) ;

REMOVE ÙN IF (0 UN) ;

REMOVE SÌ IF (0 SI) ;

SELECT Inf IF (-1C Pr) ;

REMOVE Inf IF (NOT 0 Inf/à) (0 Form/a) ;

SELECT Enc IF (-1 Inf) (0 Pro OR Enc) ;

SELECT Enc IF (-2 Inf) (-1 Enc) (0 Pro OR Enc) ;

REMOVE Enc IF (NOT -1 Inf OR Enc) ;

SELECT Det IF (0 A) (1C Pos) ;

SELECT Pro IF (0 L') (1C Fin) ;

REMOVE Pr IF (0 Det OR Pr) (-1 Pr) (NOT 1 Det) ;

SELECT Sg IF (-1 IN) (0 Lingue) ;

SELECT Inf IF (0 Fin OR Inf) (-1 Mod) ;

SELECT Fin IF (NOT -1 Mod) (0 Vene) (1 Di LINK 1 Inf) ;

REMOVE Fin IF (-1 Fin) (0/* Enc) ;
